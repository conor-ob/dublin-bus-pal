apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

Properties properties = new Properties()
if (project.rootProject.file("keys.properties").exists()) {
    properties.load(project.rootProject.file('keys.properties').newDataInputStream())
}
def googleMapsApiKey = properties.getProperty('com.google.android.geo.API_KEY')
def fabricApiKey = properties.getProperty('io.fabric.ApiKey')
def twitterConsumerKey = properties.getProperty('com.twitter.sdk.android.CONSUMER_KEY')
def twitterConsumerSecret = properties.getProperty('com.twitter.sdk.android.CONSUMER_SECRET')

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "ie.dublinbuspal.android"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode 200020 // x|xx|xx|x = major_version|minor_version|bugfix|snapshot(0)/release(1)
        versionName "2.0.2-SNAPSHOT"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [googleMapsApiKey     : "$googleMapsApiKey",
                                fabricApiKey         : "$fabricApiKey",
                                twitterConsumerKey   : "$twitterConsumerKey",
                                twitterConsumerSecret: "$twitterConsumerSecret"]
    }

    buildTypes {
        release {
            debuggable false
//            minifyEnabled true
//            shrinkResources true
//            useProguard true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix = ".debug"
            ext.enableCrashlytics = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    // xpp3 defines classes that conflict with classes now provided by Android. Solutions include
    // finding newer versions or alternative libraries that don't have the same problem (for example,
    // for httpclient use HttpUrlConnection or okhttp instead), or repackaging the library using
    // something like jarjar. [DuplicatePlatformClasses]
    configurations {
        all {
            exclude module: 'httpclient'
            exclude module: 'xpp3'
        }
    }

}

dependencies {
    implementation project(':data')
    implementation project(':database')
    implementation project(':domain')
    implementation project(":repository")
    implementation project(":service")
    implementation project(':util')

    implementation "androidx.appcompat:appcompat:${versions.androidx.appcompat}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.androidx.constraintlayout}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidx.recyclerview}"
    implementation "androidx.room:room-runtime:${versions.androidx.room_runtime}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "com.google.dagger:dagger:${versions.dagger}"
    implementation "com.google.maps.android:android-maps-utils:${versions.android_map_utils}"
    implementation "com.hannesdorfmann.mosby3:mvp-conductor:${versions.mvp_conductor}"
    implementation "com.jakewharton.threetenabp:threetenabp:${versions.threetenabp}"
    implementation "com.jakewharton.timber:timber:${versions.timber}"
    implementation "com.twitter.sdk.android:tweet-ui:$versions.tweet_ui"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rx_android}"
    implementation "io.reactivex.rxjava2:rxkotlin:${versions.rx_kotlin}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
    implementation "pl.charmas.android:android-reactive-location2:${versions.android_reactive_location2}"
    kapt "com.google.dagger:dagger-compiler:${versions.dagger_compiler}"

    androidTestImplementation "androidx.test:core:${testVersions.androidx.core}"
    androidTestImplementation "androidx.test.ext:junit:${testVersions.androidx.junit}"
    androidTestImplementation "androidx.test:runner:${testVersions.androidx.runner}"
    testImplementation "junit:junit:${testVersions.junit}"

    compile("com.crashlytics.sdk.android:crashlytics:${versions.crashlytics}") {
        transitive = true
    }

    // Conflict with dependency 'blah blah' in project ':app'. Resolved versions for runtime classpath (x.x.x) and compile classpath (x.x.x) differ. This can lead to runtime crashes
    implementation("com.squareup.retrofit2:converter-gson:2.5.0")
    implementation("com.google.code.gson:gson:2.8.5")
    implementation("com.squareup.retrofit2:converter-simplexml:2.5.0")
    implementation("com.squareup.okhttp3:okhttp:3.12.1")
}
