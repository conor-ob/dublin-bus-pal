buildscript {

    ext.buildConfig = [
            'minSdk'    : 21,
            'targetSdk' : 28,
            'compileSdk': 28,
            'version'   : [
                    'major'  : 2,
                    'minor'  : 1,
                    'patch'  : 6,
                    'release': false
            ],
    ]

    if (ext.buildConfig.version.release) {
        ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    } else {
        ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}-SNAPSHOT"
    }
    ext.buildConfig.version['code'] = buildConfig.version.major * 100000 + buildConfig.version.minor * 1000 + buildConfig.version.patch * 10

    ext.versions = [
            'androidx'                   : [
                    'appcompat'       : '1.0.2', // https://mvnrepository.com/artifact/androidx.appcompat/appcompat
                    'constraintlayout': '1.1.3', // https://mvnrepository.com/artifact/androidx.constraintlayout/constraintlayout
                    'recyclerview'    : '1.0.0', // https://mvnrepository.com/artifact/androidx.recyclerview/recyclerview
                    'room_common'     : '2.0.0', // https://mvnrepository.com/artifact/androidx.room/room-common
                    'room_compiler'   : '2.0.0', // https://mvnrepository.com/artifact/androidx.room/room-compiler
                    'room_runtime'    : '2.0.0', // https://mvnrepository.com/artifact/androidx.room/room-runtime
                    'room_rxjava2'    : '2.0.0'  // https://mvnrepository.com/artifact/androidx.room/room-rxjava2

            ],
            'android_map_utils'          : '0.5',       // https://mvnrepository.com/artifact/com.google.maps.android/android-maps-utils
            'android_reactive_location2' : '2.1',       // https://mvnrepository.com/artifact/pl.charmas.android/android-reactive-location2
            'adapter_rxjava2'            : '2.5.0',     // https://mvnrepository.com/artifact/com.squareup.retrofit2/adapter-rxjava2
            'material'                   : '1.0.0',     // https://mvnrepository.com/artifact/com.google.android.material/material
            'mvp_conductor'              : '3.1.0',     // https://mvnrepository.com/artifact/com.hannesdorfmann.mosby3/mvp-conductor
            'crashlytics'                : '2.10.0',    // https://www.fabric.io/kits/android/crashlytics/update
            'converter_gson'             : '2.5.0',     // https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-gson
            'converter_simplexml'        : '2.5.0',     // https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-simplexml
            'dagger'                     : '2.22.1',    // https://mvnrepository.com/artifact/com.google.dagger/dagger
            'dagger_compiler'            : '2.22.1',    // https://mvnrepository.com/artifact/com.google.dagger/dagger-compiler
            'fabric'                     : '1.28.1',    // https://github.com/firebase/quickstart-android/blob/master/crash/build.gradle
            'firebase_core'              : '16.0.9',    // https://mvnrepository.com/artifact/com.google.firebase/firebase-core
            'gradle'                     : '3.4.1',     // https://mvnrepository.com/artifact/com.android.tools.build/gradle?repo=google
            'google_services'            : '4.2.0',     // https://github.com/firebase/quickstart-android/blob/master/crash/build.gradle
            'gson'                       : '2.8.5',     // https://mvnrepository.com/artifact/com.google.code.gson/gson
            'java_ml'                    : '0.1.5',     // https://mvnrepository.com/artifact/net.sourceforge/javaml?repo=icm
            'kotlin'                     : '1.3.21',    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-stdlib-jdk8
            'logging_interceptor'        : '3.12.2',    // https://mvnrepository.com/artifact/com.squareup.okhttp3/logging-interceptor
            'play_services_location'     : '16.0.0',    // https://mvnrepository.com/artifact/com.google.android.gms/play-services-location
            'play_services_maps'         : '16.1.0',    // https://mvnrepository.com/artifact/com.google.android.gms/play-services-maps
            'play_services_places'       : '16.1.0',    // https://mvnrepository.com/artifact/com.google.android.gms/play-services-places
            'retrofit'                   : '2.5.0',     // https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
            'rx_android'                 : '2.1.1',     // https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxandroid
            'rx_java'                    : '2.2.8',     // https://mvnrepository.com/artifact/io.reactivex.rxjava2/rxjava
            'slf4j_api'                  : '1.7.26',    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
            'slf4j_timber'               : '1.0.1',     // https://mvnrepository.com/artifact/at.favre.lib/slf4j-timber
            'store3'                     : '3.1.1',     // https://mvnrepository.com/artifact/com.nytimes.android/store3
            'threetenabp'                : '1.2.0',     // https://mvnrepository.com/artifact/com.jakewharton.threetenabp/threetenabp
            'timber'                     : '4.7.1',     // https://mvnrepository.com/artifact/com.jakewharton.timber/timber
            'tweet_ui'                   : '3.3.0'      // https://mvnrepository.com/artifact/com.twitter.sdk.android/tweet-ui
    ]

    ext.testVersions = [
            'androidx': [
                    'core'        : '1.1.0', // https://mvnrepository.com/artifact/androidx.test/core
                    'junit'       : '1.1.0', // https://mvnrepository.com/artifact/androidx.test.ext/junit
                    'room_testing': '2.0.0', // https://mvnrepository.com/artifact/androidx.room/room-testing
                    'runner'      : '1.1.1'  // https://mvnrepository.com/artifact/androidx.test/runner?repo=google
            ],
            //'espresso_core' : '3.0.2', // https://mvnrepository.com/artifact/com.android.support.test.espresso/espresso-core
            'junit'     : '4.12',  // https://mvnrepository.com/artifact/junit/junit
            'threetenbp': '1.3.8', // https://mvnrepository.com/artifact/org.threeten/threetenbp
            'truth'     : '0.42'   // https://mvnrepository.com/artifact/com.google.truth/truth
    ]

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'http://maven.icm.edu.pl/artifactory/repo/' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.gradle}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.google_services}"
        classpath "io.fabric.tools:gradle:${versions.fabric}"
    }

}

allprojects {

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.google.com/' }
        maven { url 'http://maven.icm.edu.pl/artifactory/repo/' }
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
